# 対話シェルでも # をコメントにする
setopt interactive_comments

# port番号でプロセスをkillする
killp() {
  if [ -z "$1" ]; then
    echo "Usage: killp <port>"
    return 1
  fi
  kill -9 $(lsof -ti :"$1") 2>/dev/null && echo "Killed process on port $1" || echo "No process found on port $1"
}

# org/repo 形式または URL を受け取って ghq get する
ghq-clone() {
  # 引数チェック：何も指定がなければ使い方を表示して終了
  if [ $# -lt 1 ]; then
    echo "Usage: repo-get <org/repo> | <git URL>" >&2
    return 1
  fi

  local target="$1"

  # 引数が URL (https://... or git@...) ならそのまま、そうでなければ github.com/<org/repo> とする
  if [[ "$target" =~ ^https?:// ]] || [[ "$target" =~ ^git@ ]]; then
    ghq get "$target"
  else
    ghq get "github.com/${target}"
  fi
}

# ghq で管理しているリポジトリをfzfで選択し、引数で指定したコマンドで実行する
# 選択履歴を保存して次回は優先表示する
ghq-select() {
  # 開くコマンド（第一引数があればそれを、なければ code）
  local opener="${1:-code}"

  # 選択履歴保存先
  local histfile="$HOME/.config/.ghq_fzf_history"
  # ghq root
  local ghq_root
  ghq_root=$(ghq root | head -n1) || return
  # fzf の選択結果 (ghq rootからの相対)         
  local rel     
  # 絶対パス
  local abs

  # fzf 用オプションを配列で定義
  local -a fzf_opts=(
    # 最新を上に表示
    --reverse
    # プレビューに README.md の最初の200行を表示
    --preview="head -n200 ${ghq_root}/{1}/README.md 2>/dev/null || echo 'No README.md'"
  )

  # 1) 選択結果を rel に代入
  rel=$(
    # サブシェル開始：履歴と ghq list を結合
    (
      # ヒストリーファイルがあれば、新しいものから逆順に出力
      [ -f "$histfile" ] && tac "$histfile"
      # ghq 管理下のリポジトリを相対パスで一覧表示
      ghq list
    ) |
    # 重複行を除去し、初出の行だけを通過させる
    awk '!seen[$0]++' |
    # fzf でインタラクティブに選択
    fzf "${fzf_opts[@]}"
  ) || return  # キャンセル時は関数を終了

  # 2) 履歴に追記
  echo "$rel" >> "$histfile"

  # 3) 引数で指定したコマンドで開く
  "$opener" "${ghq_root}/${rel}"
}
